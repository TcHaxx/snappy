<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_Snappy" Id="{fab0c229-8c37-418d-9cd2-a63405d9a539}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'no_explicit_call' := 'do not call this POU directly'}
FUNCTION_BLOCK FINAL FB_Snappy IMPLEMENTS I_SnappySetup, I_SnappySetupConfigBuild, I_SnappyVerify
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR

VAR
//	{attribute 'hide'}
	fbConfig					: FB_SnappyConfig;
	itfConfig					: I_SnappyConfig := fbConfig; 
	fbTestSuiteNameProvider		: FB_TestSuiteNameProvider;
	itfTestFrameWork			: I_TestFrameWorkAdapter := fbTestSuiteNameProvider;
	stVerificationResultField 	: ST_VerificationResult;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="ITFs" Id="{f11ae277-eee9-4f23-b5ea-8337294c20e3}" />
    <Method Name="Build" Id="{695770de-f3f3-4ad9-9233-3e949aed9190}" FolderPath="ITFs\">
      <Declaration><![CDATA[METHOD Build : HRESULT
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Build := itfConfig.Build();

IF FAILED(Build) THEN
	RETURN;
END_IF

IF THIS^.itfConfig.itfTestFramework <> NULL THEN
	THIS^.itfTestFrameWork := THIS^.itfConfig.itfTestFramework;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Configure" Id="{aed1d2aa-f88b-4f94-ab82-85cd02d987ed}" FolderPath="ITFs\">
      <Declaration><![CDATA[METHOD Configure : I_SnappySetupDefaultsOrRpc
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Configure := THIS^.itfConfig.Configure();]]></ST>
      </Implementation>
    </Method>
    <Property Name="stVerificationResult" Id="{8ef910c8-b6d4-4ea6-ae03-8960790a65f6}">
      <Declaration><![CDATA[PROPERTY stVerificationResult : REFERENCE TO ST_VerificationResult
]]></Declaration>
      <Get Name="Get" Id="{6789235d-1324-4469-ba04-c14014178dff}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[stVerificationResult REF= THIS^.stVerificationResultField;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Verify" Id="{ab7f32c4-6140-4125-99c7-1a8cd2d6bf2a}" FolderPath="ITFs\">
      <Declaration><![CDATA[METHOD Verify : HRESULT
VAR_INPUT
	anyArg	: ANY;
END_VAR

VAR
	hr 		: HRESULT;
END_VAR


VAR_INST
	sJDoc			: T_VerifyContent;
	eState			: E_SnappyVerificationState;
	hrLastErrorCode : HRESULT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT itfConfig.bIsConfigured THEN
	Verify := Tc2_system.E_HRESULTAdsErr.NOTREADY;
	RETURN;
END_IF

CASE eState OF
	
	E_SnappyVerificationState.eIdle:
	itfConfig.itfRpcProxy.itfTestFrameWorkAdapter := THIS^.itfConfig.itfTestFramework;
	hr := itfConfig.itfSerializer.Serialize(anyArg:= anyArg, sJsonDocument:= sJDoc);
	IF FAILED(hr) THEN
		hrLastErrorCode := hr;
		eState := E_SnappyVerificationState.eError;
	ELSE
		hrLastErrorCode := S_PENDING;
		eState := E_SnappyVerificationState.eSend;
	END_IF

	E_SnappyVerificationState.eSend:		
		hr := itfConfig.itfRpcProxy.Verify(sToVerify:= sJDoc);
		IF SUCCEEDED(hr) THEN
			eState := E_SnappyVerificationState.ePending;
		ELSIF FAILED(hr) THEN
			hrLastErrorCode := hr;
			eState := E_SnappyVerificationState.eError;
		END_IF
		
	E_SnappyVerificationState.ePending:
		hr := itfConfig.itfRpcProxy.GetVerificationResult(stResult => stVerificationResultField);
		hrLastErrorCode := hr;
		IF SUCCEEDED_AND_NOT_PENDING(hr) THEN
			eState := E_SnappyVerificationState.eDone;
		ELSIF FAILED(hr) THEN
			eState := E_SnappyVerificationState.eError;
		END_IF

	E_SnappyVerificationState.eDone:
		eState := E_SnappyVerificationState.eIdle;

	E_SnappyVerificationState.eError:
		eState := E_SnappyVerificationState.eIdle;
		
END_CASE

Verify := hrLastErrorCode;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>